plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}
repositories {
    maven { url = "https://maven.shedaniel.me/" }
    maven { name = 'TerraformersMC'; url = 'https://maven.terraformersmc.com/releases/' }
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation("com.terraformersmc:modmenu:${mod_menu_version}")
    // Use the Fabric artifact of Cloth Config for Fabric environment
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${fabric_cloth_config_version}"
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}
